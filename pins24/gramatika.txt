program -> definition definition2 .
definition -> FUN IDENTIFIER LPAREN parameters RPAREN definition3 .
definition -> VAR IDENTIFIER ASSIGN initializers .
definition2 -> definition definition2 .
definition2 -> .
definition3 -> .
definition3 -> ASSIGN statements .

parameters -> .
parameters -> IDENTIFIER parameters2 .
parameters2 -> COMMA parameters .
parameters2 -> .

statements -> statement statements2 .
statements2 -> COMMA statements .
statements2 -> .
statement -> expression expression2 .
statement -> IF expression THEN statements statementElse END .
statement -> WHILE expression DO statements END .
statement -> LET definicije IN statements END .
statementElse -> ELSE statements .
statementElse -> .
expression2 -> ASSIGN expression .
expression2 -> .

definicije -> definition definicije2 .
definicije2 -> definicije .
definicije2 -> .

expression -> eAnd eOR .
eOR -> OR eAnd eOR .
eOR -> .

eAnd -> eComp eAnd2 .
eAnd2 -> AND eComp eAnd2 .
eAnd2 -> .

eComp -> eAdit eComp2 .
eComp2 -> EQU eAdit .
eComp2 -> NEQ eAdit .
eComp2 -> GTH eAdit .
eComp2 -> LTH eAdit .
eComp2 -> GEQ eAdit .
eComp2 -> LEQ eAdit .
eComp2 -> .

eAdit -> eMul eAdit2 .
eAdit2 -> ADD eMul eAdit2 .
eAdit2 -> SUB eMul eAdit2 .
eAdit2 -> .

eMul -> ePrefix eMul2 .
eMul2 -> MUL ePrefix eMul2 .
eMul2 -> DIV ePrefix eMul2 .
eMul2 -> MOD ePrefix eMul2 .
eMul2 -> .

ePrefix -> prefix ePostfix .
prefix -> prefix2 .
prefix2 -> prefixOp prefix2 .
prefix2 -> .
prefixOp -> NOT .
prefixOp -> ADD .
prefixOp -> SUB .
prefixOp -> PTR .

ePostfix -> eKoncni postfix .
postfix -> PTR postfix .
postfix -> .

eKoncni -> INTCONST .
eKoncni -> CHARCONST .
eKoncni -> STRINGCONST .
eKoncni -> IDENTIFIER args .
eKoncni -> LPAREN expression RPAREN .
args -> LPAREN arguments RPAREN .
args -> .

arguments -> .
arguments -> expression arguments2 .
arguments2 -> COMMA expression arguments2 .
arguments2 -> .

initializers -> .
initializers -> initializer initializers2 .
initializers2 -> COMMA initializer initializers2 .
initializers2 -> .

initializer -> CHARCONST .
initializer -> STRINGCONST .
initializer -> INTCONST initializer2 .
initializer2 -> .
initializer2 -> MUL const .

const -> INTCONST .
const -> CHARCONST .
const -> STRINGCONST .